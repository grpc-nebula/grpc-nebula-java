apply plugin: 'com.github.kt3k.coveralls'

description = "gRPC: All"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url INTRANET_MAVEN_URL
            credentials {
                username INTRANET_MAVEN_USERNAME
                password INTRANET_MAVEN_PASSWORD
            }
        }
    }
    dependencies { classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1' }
}

def subprojects = [
    project(':orientsec-grpc-auth'),
    project(':orientsec-grpc-core'),
    project(':orientsec-grpc-context'),
    project(':orientsec-grpc-netty'),
    project(':orientsec-grpc-okhttp'),
    project(':orientsec-grpc-protobuf'),
    project(':orientsec-grpc-protobuf-lite'),
    project(':orientsec-grpc-protobuf-nano'),
    project(':orientsec-grpc-stub'),
    project(':orientsec-grpc-testing'),
]

for (subproject in rootProject.subprojects) {
    if (subproject == project) {
        continue
    }
    evaluationDependsOn(subproject.path)
}

dependencies {
    compile subprojects.minus(project(':orientsec-grpc-protobuf-lite'))
}

javadoc {
    classpath = files(subprojects.collect { subproject ->
        subproject.javadoc.classpath
    })
    for (subproject in subprojects) {
        if (subproject == project) {
            continue;
        }
        source subproject.javadoc.source
        options.links subproject.javadoc.options.links.toArray(new String[0])
    }
}

task jacocoMerge(type: JacocoMerge) {
    dependsOn(subprojects.jacocoTestReport.dependsOn)
    mustRunAfter(subprojects.jacocoTestReport.mustRunAfter)
    destinationFile = file("${buildDir}/jacoco/test.exec")
    executionData = files(subprojects.jacocoTestReport.executionData)
            .plus(project(':orientsec-grpc-interop-testing').jacocoTestReport.executionData)
            .filter { f -> f.exists() }
}

jacocoTestReport {
    dependsOn(jacocoMerge)
    reports {
        xml.enabled = true
        html.enabled = true
    }

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    classDirectories = files(classDirectories.files.collect {
        fileTree(dir: it,
        exclude: [
            '**/io/grpc/okhttp/internal/**'
        ])
    })
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
}

tasks.coveralls { dependsOn(jacocoTestReport) }
